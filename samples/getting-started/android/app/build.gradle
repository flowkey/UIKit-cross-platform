apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 30
    defaultConfig {
        applicationId "com.example"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }
    splits {
        abi {
            reset()
            enable true
            universalApk false
            include "arm64-v8a", "x86_64"
        }
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
}

task buildSwiftArm64(type: Exec) {
    commandLine 'sh', '-c', getSwiftBuildCommand('arm64-v8a')
}

task buildSwiftIntel64(type: Exec) {
    commandLine 'sh', '-c', getSwiftBuildCommand('x86_64')
}

task cleanSwift(type: Exec) {
    commandLine 'sh', '-c', "rm -rf ../../build"
}

preBuild.dependsOn buildSwiftArm64
preBuild.dependsOn buildSwiftIntel64

clean.dependsOn cleanSwift

def getSwiftBuildCommand(abi) {
    def cmakeBuildType = "RelWithDebInfo"
    def buildScript = "../../../../swift-android-toolchain/swift-build.sh"
    return "CMAKE_BUILD_TYPE=$cmakeBuildType ANDROID_ABI=$abi LIBRARY_OUTPUT_DIRECTORY='${projectDir}/src/main/jniLibs/${abi}' $buildScript ../../"
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    api project(":uikit")
}
